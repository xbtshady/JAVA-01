## 四则运算
Java代码
```java
public class Hello {
    public static void main(String[] args) {
        int a = 1,b=1,c;
        c= a+b;
        c= a-b;
        c= a*b;
        c= a/b;
    }
}
```
反编译后的字节码：
```
public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=4, args_size=1
         0: iconst_1    //int类型1入栈，栈顶=1
         1: istore_1    //将栈顶的int数值出栈并存入第二个局部变量
         2: iconst_1   //int类型1入栈，栈顶=1
         3: istore_2   //将栈顶的int数值出栈并存入第三个局部变量
         4: iload_1    //将第二个局部变量值压入栈顶
         5: iload_2    //将第三个局部变量值压入栈顶
         6: iadd         //将栈顶两int类型数相加，结果入栈
         7: istore_3   //将栈顶的int数值出栈并存入第四个局部变量
         8: iload_1  //将第二个局部变量值压入栈顶
         9: iload_2  //将第三个局部变量值压入栈顶
        10: isub      //将栈顶两int类型数相减，结果入栈
        11: istore_3 //将栈顶的int数值出栈并存入第四个局部变量
        12: iload_1 //将第二个局部变量值压入栈顶
        13: iload_2 //将第三个局部变量值压入栈顶
        14: imul  //将栈顶两int类型数相乘，结果入栈
        15: istore_3   //将栈顶的int数值出栈并存入第四个局部变量
        16: iload_1 //将第二个局部变量值压入栈顶
        17: iload_2 //将第三个局部变量值压入栈顶
        18: idiv  //将栈顶两int类型数相除，结果入栈
        19: istore_3  //将栈顶的int数值出栈并存入第四个局部变量
        20: return
      LineNumberTable:
        line 3: 0
        line 4: 4
        line 5: 8
        line 6: 12
        line 7: 16
        line 8: 20
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      21     0  args   [Ljava/lang/String;
            2      19     1     a   I
            4      17     2     b   I
            8      13     3     c   I
```
这次的运算，基本逻辑就是int值入栈(iconst)，int值出栈(istore)，完成变量a、b的声明和初始化，然后将a,b的值压入栈中，使用iadd，isub，imul，idiv进行四则运算，然后istore_3将结果存入c

其中要注意的是，store会删除栈顶值，load将局部变量压入操作数栈，不会删除局部变量中的值

## if 和for
Java代码
```
import java.util.ArrayList;
import java.util.List;

public class Hello {
    public static void main(String[] args) {
        List<Integer> list = new ArrayList<Integer>();
        for(int i = 0;i<10;i++){
            list.add(i);
        }
        for(Integer a:list){
            if(a%2 == 0){
                System.out.println(a);
            }
        }
    }
}
```
反编译之后的字节码
```
public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=4, args_size=1
         0: new           #2                  // class java/util/ArrayList
         3: dup
         4: invokespecial #3                  // Method java/util/ArrayList."<init>":()V
         7: astore_1
         8: iconst_0
         9: istore_2
        10: iload_2
        11: bipush        10
        13: if_icmpge     33
        16: aload_1
        17: iload_2
        18: invokestatic  #4                  // Method java/lang/Integer.valueOf:(I)Ljava/lang/Integer;
        21: invokeinterface #5,  2            // InterfaceMethod java/util/List.add:(Ljava/lang/Object;)Z
        26: pop
        27: iinc          2, 1
        30: goto          10
        33: aload_1
        34: invokeinterface #6,  1            // InterfaceMethod java/util/List.iterator:()Ljava/util/Iterator;
        39: astore_2
        40: aload_2
        41: invokeinterface #7,  1            // InterfaceMethod java/util/Iterator.hasNext:()Z
        46: ifeq          78
        49: aload_2
        50: invokeinterface #8,  1            // InterfaceMethod java/util/Iterator.next:()Ljava/lang/Object;
        55: checkcast     #9                  // class java/lang/Integer
        58: astore_3
        59: aload_3
        60: invokevirtual #10                 // Method java/lang/Integer.intValue:()I
        63: iconst_2
        64: irem
        65: ifne          75
        68: getstatic     #11                 // Field java/lang/System.out:Ljava/io/PrintStream;
        71: aload_3
        72: invokevirtual #12                 // Method java/io/PrintStream.println:(Ljava/lang/Object;)V
        75: goto          40
        78: return
      LineNumberTable:
        line 6: 0
        line 7: 8
        line 8: 16
        line 7: 27
        line 10: 33
        line 11: 59
        line 12: 68
        line 14: 75
        line 15: 78
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
           10      23     2     i   I
           59      16     3     a   Ljava/lang/Integer;
            0      79     0  args   [Ljava/lang/String;
            8      71     1  list   Ljava/util/List;
```
上面的是完整的字节码下面一段段的分析
这一段字节码实现了第一个循环
```
8: iconst_0
9: istore_2
10: iload_2
11: bipush        10
13: if_icmpge     33
16: aload_1
17: iload_2
18: invokestatic  #4                  // Method java/lang/Integer.valueOf:(I)Ljava/lang/Integer;
21: invokeinterface #5,  2            // InterfaceMethod java/util/List.add:(Ljava/lang/Object;)Z
26: pop
27: iinc          2, 1
30: goto          10
```
8-10行生成i变量并赋值为0，在将改变量压入栈顶，然后11行将10压入栈顶，在13行比较栈顶两个int的值大于等于后则跳转到33行，如果是小于的话接着到16行-26行，进行list的app的操作，然后使用27行的iiinc实现i的自增，最后是30行的goto实现跳转到第10行完成一次循环。

然后是第二个循环加判断
```
33: aload_1
34: invokeinterface #6,  1            // InterfaceMethod java/util/List.iterator:()Ljava/util/Iterator;
39: astore_2
40: aload_2
41: invokeinterface #7,  1            // InterfaceMethod java/util/Iterator.hasNext:()Z
46: ifeq          78
49: aload_2
50: invokeinterface #8,  1            // InterfaceMethod java/util/Iterator.next:()Ljava/lang/Object;
55: checkcast     #9                  // class java/lang/Integer
58: astore_3
59: aload_3
60: invokevirtual #10                 // Method java/lang/Integer.intValue:()I
63: iconst_2
64: irem
65: ifne          75
68: getstatic     #11                 // Field java/lang/System.out:Ljava/io/PrintStream;
71: aload_3
72: invokevirtual #12                 // Method java/io/PrintStream.println:(Ljava/lang/Object;)V
75: goto          40
78: return
```
这段代码实现了第二个循环加判断，这个地方用的是迭代器实现的循环，33-46行主要是通过调用迭代器的hasNext判断循环是否结束，如果结束了，46行可直接跳到78行结束循环，这个循环在75行使用goto跳到40行执行下一次循环

判断的地方首先是，49-64行首先是49-58 Interger的强转并压入栈中，然后是63-64的与2取模运算，然后是65行的ifne对计算结果也就是栈顶的判断是否为0，不为0则跳转到75行也就是条件不成立，这样就完成了条件判断if